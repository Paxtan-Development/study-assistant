/*
 * Copyright 2020 PC Chin. All rights reserved.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

// Load keystore
def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file('keystore.properties')
    // Put isLocal first so that it can be overwritten later within keystore.properties itself (If needed)
    keystoreProperties.put('isLocal', 'true')
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ignored) {
    keystoreProperties.put('keystoreDir', '/')
    keystoreProperties.put('isLocal', 'false')
}

android {
    signingConfigs {
        release {
            storeFile = file(keystoreProperties['keystoreDir'])
            keyAlias = keystoreProperties['keystoreAlias']
            keyPassword = keystoreProperties['keystorePass']
            storePassword = keystoreProperties['keystorePass']
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.pcchin.studyassistant"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 16
        versionName "2.0.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        buildConfigField "boolean", "IS_LOCAL", "${(boolean) keystoreProperties.getProperty('isLocal')}"
        buildConfigField "String", "SENTRY_DSN", "${keystoreProperties.getProperty('sentryDsn')}"
        resValue "string", "app_name", "Study Assistant"
    }
    buildTypes {
        /** Version differences:
         * Debug: Used for local testing, does not send data to server, needs to be built and installed manually
         * Alpha: Used for quick testing, sends data to server, needs to be built and installed manually
         * Beta: Used for slower testing, sends data to server, can be installed through APK
         * Release: The release version of the app, can be installed through APK **/
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            versionNameSuffix = '-d'
            sourceSets {
                androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
            }
            resValue "string", "app_name", "SA Debug"
        }
        alpha {
            applicationIdSuffix ".alpha"
            debuggable true
            versionNameSuffix = '-a'
            sourceSets {
                androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
            }
            resValue "string", "app_name", "SA Alpha"
        }
        beta {
            initWith release
            applicationIdSuffix ".beta"
            versionNameSuffix = '-b'
            resValue "string", "app_name", "SA Beta"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
    buildToolsVersion = '29.0.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha03'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'com.android.support:multidex:1.0.3'

    // Test implementations
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0-rc01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-rc01'

    // Room implementation
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'

    // Own libraries
    implementation 'com.pcchin.customdialog:customdialog:1.0.2'
    implementation 'com.pcchin.licenseview:licenseview:1.3.1'
    implementation 'com.pcchin.dtpreference:dtpreference:1.1.1'

    // External implemented libs (.com)
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.dhaval2404:imagepicker:1.7.1'
    implementation 'com.jaredrummler:android-device-names:2.0.0'

    // External implemented libs (.net)
    implementation 'net.lingala.zip4j:zip4j:2.5.2'

    // External implemented libs (.org)
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    // External implemented libs (.io)
    implementation 'io.sentry:sentry:1.7.30'
}
